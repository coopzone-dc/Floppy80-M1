.program fdc
.side_set 1 opt

///////////////////////////////////////////////////////////
// API documentions is located at
// https://raspberrypi.github.io/pico-sdk-doxygen/
///////////////////////////////////////////////////////////

.wrap_target
start:
    nop side 0          // deactivate WAIT

    // wait for IN, RD, WR, OUT and MREQ to go high (inactive)
    set y, 31           // input compare for IN, RD, WR, OUT and MREQ = 1 (0x1F)
wait_mreq:
    in pins, 5          // shift state of 5 input pins (IN, RD, WR, OUT and MREQ) into isr
    in null, 27         // shift IN, RD, WR, OUT and MREQ to lowest 5 bits of isr, with remaining bits zero
    mov x, isr
    jmp x!=y wait_mreq

    // push 31 to notify application IN, RD, WR, OUT and MREQ are all high
    push

    // wait for MREQ to be low (active)
    wait 0 pin 4

    nop side 1          // activate WAIT

    set y, 3            // "set y, 3" - sets y to compare value for RD and WR

wait_rdwr:
    // wait for RD or WR to go low
    in pins, 3          // shift state of 3 input pins (IN, RD, WR) into isr
    in null, 30         // rotate RD to bit 0, WR to bit 1 (shift out IN) and remain bits zero/null
    mov x, isr
    jmp x!=y rdwr_low
    jmp wait_rdwr

rdwr_low:
    // push 1 for RD or 2 for WR into fifo to tell memory task RD or WR are low
    push

stall:
    jmp stall

.wrap

% c-sdk {

void fdc_program_init(PIO pio, uint sm, uint offset, pio_sm_config* pc) {
    uint in_pin = 10;      // GPIO number for first input of an "in" operation
                           // "in pins" for the five inputs (IN, RD, WR, OUT and MREQ)
    uint sideset_pin = 1;  // GPIO number that is used in a side set operation (the WAIT signal)

    *pc = fdc_program_get_default_config(offset);

    pio_gpio_init(pio, in_pin);
    pio_gpio_init(pio, sideset_pin);

    sm_config_set_in_pins(pc, in_pin);               // first pin associated with the in instruction
    sm_config_set_sideset_pins(pc, sideset_pin);     // first pin associated with the side option

    sm_config_set_clkdiv(pc, 1);

    pio_sm_set_consecutive_pindirs(pio, sm, in_pin, 5, false);          // specify the first and number of input pins
    pio_sm_set_consecutive_pindirs(pio, sm, sideset_pin, 1, true);      // specify the first and number of output pins

    pio_sm_init(pio, sm, offset, pc);
}

%}